/*
 * Copyright (c) 2007-2015 Concurrent, Inc. All Rights Reserved.
 *
 * Project and contact information: http://www.cascading.org/
 *
 * This file is part of the Cascading project.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import java.text.SimpleDateFormat

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'provided-base'
apply plugin: 'com.github.johnrengelman.shadow'

ext.cascadingVersion = '3.0.0-wip-+'

apply from: "etc/properties.gradle"
apply from: "etc/version.gradle"

group = 'cascading'
version = releaseVersion

ext.timestamp = new SimpleDateFormat( "yyyyMMdd" ).format( new Date() )

buildscript {
  repositories {
    mavenCentral()
    jcenter()
  }

  dependencies {
    classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.1'
    classpath 'com.netflix.nebula:gradle-extra-configurations-plugin:2.2.1'
  }
}

configurations {
  testArtifacts {
    extendsFrom testRuntime
  }
}

repositories {
  mavenLocal()
  mavenCentral()
  maven{ url 'http://conjars.org/repo/' }
  maven{ url 'https://repository.apache.org/content/repositories/releases/' }
  maven{ url 'http://files.couchbase.com/maven2/' }
}

dependencies {
  compile( 'net.spy:spymemcached:2.11.7' )

  provided group: 'cascading', name: 'cascading-core', version: cascadingVersion, changing: true
  provided group: 'org.slf4j', name: 'slf4j-api', version: '1.7.5'
  provided group: 'org.slf4j', name: 'slf4j-log4j12', version: '1.7.5'
  provided group: 'log4j', name: 'log4j', version: '1.2.17'

  testCompile group: 'cascading', name: 'cascading-local', version: cascadingVersion, changing: true
  testCompile group: 'cascading', name: 'cascading-hadoop', version: cascadingVersion, changing: true
  testCompile group: 'cascading', name: 'cascading-core', version: cascadingVersion, classifier: 'tests', changing: true
  testCompile group: 'cascading', name: 'cascading-local', version: cascadingVersion, classifier: 'tests', changing: true
  testCompile group: 'cascading', name: 'cascading-hadoop', version: cascadingVersion, classifier: 'tests', changing: true
  testCompile group: 'cascading', name: 'cascading-platform', version: cascadingVersion, classifier: 'tests', changing: true
  testCompile( 'com.thimbleware.jmemcached:jmemcached-core:1.0.0' )
  testCompile( 'junit:junit:4.12' )
}

task sourcesJar( type: Jar, dependsOn: classes ) {
  from sourceSets.main.allSource
  classifier = 'sources'
}

task javadocJar( type: Jar, dependsOn: javadoc ) {
  classifier = 'javadoc'
  from javadoc.destinationDir
}

task testsJar( type: Jar, dependsOn: testClasses ) {
  from sourceSets.test.output
  classifier = 'tests'
}

task testSourcesJar( type: Jar, dependsOn: classes ) {
  from sourceSets.test.allSource
  classifier = 'test-sources'
}

  shadowJar {
    classifier = "provider"
    configurations = []
    from {
      project.configurations.runtime.minus( [ project.configurations.provided ] )
    }
    mergeServiceFiles()

    exclude 'LICENSE'
    exclude 'META-INF/*.DSA'
    exclude 'META-INF/LICENSE*'
  }

artifacts {
  archives jar
  archives shadowJar
  archives sourcesJar
  archives javadocJar
  archives testsJar
  archives testSourcesJar
  testArtifacts testsJar
  testArtifacts testSourcesJar
}

jar {
  manifest {
    attributes( "Build-Date": "${timestamp}" )
  }
}

test {
  jvmArgs '-Xmx756m'

  enableAssertions = false

  include '**/*Test.class'
}

uploadArchives {

  repositories.mavenDeployer {
    configuration = configurations.archives

    repository( url: repoUrl ) {
      authentication( userName: repoUserName, password: repoPassword )
    }

    pom.project {
      description 'Cascading Memcached is a simple adapter for Memcached.'
      inceptionYear '2012'
      url 'http://cascading.org/'
      scm {
        url 'https://github.com/Cascading/cascading-memcached.git'
      }
      licenses {
        license {
          name 'The Apache Software License, Version 2.0'
          url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
          distribution 'repo'
        }
      }
    }
  }
}
