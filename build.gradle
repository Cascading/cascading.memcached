/*
 * Copyright (c) 2007-2013 Concurrent, Inc. All Rights Reserved.
 *
 * Project and contact information: http://www.cascading.org/
 *
 * This file is part of the Cascading project.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import java.text.SimpleDateFormat

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'idea'
apply plugin: 'eclipse'

ext.cascadingVersion = '2.2.0-+'

apply from: "etc/properties.gradle"

def versionProperties = new Properties()
file( 'version.properties' ).withInputStream { versionProperties.load( it ) }

group = 'cascading'
version = versionProperties[ 'cascading.memcached.version' ]

ext.timestamp = new SimpleDateFormat( "yyyyMMdd" ).format( new Date() )

configurations {
  testArtifacts {
    extendsFrom testRuntime
  }
}

repositories {
  mavenLocal()
  mavenCentral()
  mavenRepo name: 'conjars', url: 'http://conjars.org/repo/'
  mavenRepo name: 'apache', url: 'https://repository.apache.org/content/repositories/releases/'
  mavenRepo name: 'Spy Repository', url: 'http://files.couchbase.com/maven2/'
}

dependencies {
  compile group: 'cascading', name: 'cascading-core', version: cascadingVersion, changing: true
  compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.5'
  compile group: 'org.slf4j', name: 'slf4j-log4j12', version: '1.7.5'
  compile group: 'log4j', name: 'log4j', version: '1.2.17'
  compile( 'spy:spymemcached:2.8.1' )

  testCompile group: 'cascading', name: 'cascading-local', version: cascadingVersion, changing: true
  testCompile group: 'cascading', name: 'cascading-hadoop', version: cascadingVersion, changing: true
  testCompile group: 'cascading', name: 'cascading-core', version: cascadingVersion, classifier: 'tests', changing: true
  testCompile group: 'cascading', name: 'cascading-local', version: cascadingVersion, classifier: 'tests', changing: true
  testCompile group: 'cascading', name: 'cascading-hadoop', version: cascadingVersion, classifier: 'tests', changing: true
  testCompile group: 'cascading', name: 'cascading-platform', version: cascadingVersion, classifier: 'tests', changing: true
  testCompile( 'com.thimbleware.jmemcached:jmemcached-core:1.0.0' )
  testCompile( 'junit:junit:4.11' )
}

task sourcesJar( type: Jar, dependsOn: classes ) {
  from sourceSets.main.allSource
  classifier = 'sources'
}

task javadocJar( type: Jar, dependsOn: javadoc ) {
  classifier = 'javadoc'
  from javadoc.destinationDir
}

task testsJar( type: Jar, dependsOn: testClasses ) {
  from sourceSets.test.output
  classifier = 'tests'
}

task testSourcesJar( type: Jar, dependsOn: classes ) {
  from sourceSets.test.allSource
  classifier = 'test-sources'
}

artifacts {
  archives jar
  archives sourcesJar
  archives javadocJar
  archives testsJar
  archives testSourcesJar
  testArtifacts testsJar
  testArtifacts testSourcesJar
}

jar {
  manifest {
    attributes( "Build-Date": "${timestamp}" )
  }
}

test {
  jvmArgs '-Xmx756m'

  enableAssertions = false

  include '**/*Test.class'
}

uploadArchives {

  repositories.mavenDeployer {
    configuration = configurations.archives

    repository( url: repoUrl ) {
      authentication( userName: repoUserName, password: repoPassword )
    }

    pom.project {
      description 'Cascading Memcached is a simple adapter for Memcached.'
      inceptionYear '2012'
      url 'http://cascading.org/'
      scm {
        url 'https://github.com/Cascading/cascading-memcached.git'
      }
      licenses {
        license {
          name 'The Apache Software License, Version 2.0'
          url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
          distribution 'repo'
        }
      }
    }
  }
}